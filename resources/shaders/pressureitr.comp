#version 460 core
layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout (binding = 0) writeonly restrict uniform image3D outPressureAndDivergence;


uniform sampler3D inPressureAndDivergence;
uniform sampler3D inCollision;


uniform float delta;

float getSample(vec3 pos, float current)
{
    if(texture(inCollision, pos).w > 0.)
    {
        return current;
    }
    return texture(inPressureAndDivergence, pos).x;
}

void main()
{
    vec3 res = vec3(gl_WorkGroupSize*gl_NumWorkGroups);
    vec3 pos = (vec3(gl_GlobalInvocationID.xyz)+.5)/res;
    if(texture(inCollision, pos).w > 0.)
    {
        return;
    }
    vec2 current = texture(inPressureAndDivergence, pos).xy;
    float p1 = getSample( vec3(gl_GlobalInvocationID.xyz+vec3(-1,0,0)+.5)/res, current.x);
    float p2 = getSample( vec3(gl_GlobalInvocationID.xyz+vec3(1,0,0)+.5)/res, current.x);
    float p3 = getSample( vec3(gl_GlobalInvocationID.xyz+vec3(0,-1,0)+.5)/res, current.x);
    float p4 = getSample( vec3(gl_GlobalInvocationID.xyz+vec3(0,1,0)+.5)/res, current.x);
    float p5 = getSample( vec3(gl_GlobalInvocationID.xyz+vec3(0,0,-1)+.5)/res, current.x);
    float p6 = getSample( vec3(gl_GlobalInvocationID.xyz+vec3(0,0,1)+.5)/res, current.x);

    float pressure = (current.y+p1+p2+p3+p4+p5+p6)/6.;
    imageStore(outPressureAndDivergence, ivec3(gl_GlobalInvocationID.xyz), vec4(pressure, current.y,0,0));
}