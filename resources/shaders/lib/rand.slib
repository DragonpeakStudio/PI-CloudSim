// https://www.pcg-random.org/
uint pcg(uint v)
{
        uint state = v * 747796405u + 2891336453u;
        uint word = ((state >> ((state >> 28u) + 4u)) ^ state) * 277803737u;
        return (word >> 22u) ^ word;
}

uvec2 pcg2d(uvec2 v)
{
    v = v * 1664525u + 1013904223u;

    v.x += v.y * 1664525u;
    v.y += v.x * 1664525u;

    v = v ^ (v>>16u);

    v.x += v.y * 1664525u;
    v.y += v.x * 1664525u;

    v = v ^ (v>>16u);

    return v;
}

// http://www.jcgt.org/published/0009/03/02/
uvec3 pcg3d(uvec3 v) {

    v = v * 1664525u + 1013904223u;

    v.x += v.y*v.z;
    v.y += v.z*v.x;
    v.z += v.x*v.y;

    v ^= v >> 16u;

    v.x += v.y*v.z;
    v.y += v.z*v.x;
    v.z += v.x*v.y;

    return v;
}

// http://www.jcgt.org/published/0009/03/02/
uvec4 pcg4d(uvec4 v)
{
    v = v * 1664525u + 1013904223u;

    v.x += v.y*v.w;
    v.y += v.z*v.x;
    v.z += v.x*v.y;
    v.w += v.y*v.z;

    v ^= v >> 16u;

    v.x += v.y*v.w;
    v.y += v.z*v.x;
    v.z += v.x*v.y;
    v.w += v.y*v.z;

    return v;
}

float fpcg(float v)
{
    return float(pcg(int(v)))*(1.0/float(0xffffffffU));
}
vec2 fpcg2d(vec2 v)
{
    return vec2(pcg2d(uvec2(v)))*(1.0/float(0xffffffffU));
}
vec3 fpcg3d(vec3 v)
{
    return vec3(pcg3d(uvec3(v)))*(1.0/float(0xffffffffU));
}
vec4 fpcg4d(vec4 v)
{
    return vec4(pcg4d(uvec4(v)))*(1.0/float(0xffffffffU));
}
